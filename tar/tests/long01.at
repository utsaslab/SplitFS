# Process this file with autom4te to create testsuite. -*- Autotest -*-

# Test suite for GNU tar.
# Copyright 2005-2007, 2013-2014, 2016-2017 Free Software Foundation,
# Inc.

# This file is part of GNU tar.

# GNU tar is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.

# GNU tar is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

# In GNU format, when extracting or listing a file member with a name
# whose length is divisible by block size (512) tar used to read an
# extra block of data. In consequence the following file was not extracted.
# Reported by Josef Bauer
# References: <200501122145.j0CLjGhl006070@uhu.mchp.siemens.de>
#             http://lists.gnu.org/archive/html/bug-tar/2005-01/msg00038.html

AT_SETUP([long file names divisible by block size])
AT_KEYWORDS([longname long512])

dnl Create a directory structure with maximum directory name length 512-16
m4_pushdef([NAME],[0123456789abcde])
m4_pushdef([FULLNAME],NAME)
m4_for([N],0,29,,[m4_define([FULLNAME],FULLNAME/NAME)])

AT_TAR_CHECK([
AT_TAR_MKHIER(FULLNAME,NAME)
echo test > endfile

tar cf archive FULLNAME/NAME endfile
tar tf archive],
[0],
[FULLNAME/NAME
endfile
],
[],[],[],[gnu,oldgnu])

m4_popdef([NAME])
m4_popdef([FULLNAME])
AT_CLEANUP
